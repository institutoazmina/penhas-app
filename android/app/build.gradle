plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id'com.google.firebase.crashlytics'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    throw GradleException("versionCode not found. Define flutter.versionCode in the local.properties file.")
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    throw GradleException("versionName not found. Define flutter.versionName in the local.properties file.")
}
def secretsPropertiesFile = rootProject.file("secrets.properties")
def secretsProperties = new Properties()
if (secretsPropertiesFile.exists()){
    secretsProperties.load(new FileInputStream(secretsPropertiesFile))
}



def isFirebaseDistribution = System.getenv('IS_FIREBASE_DISTRIBUTION') == 'true'


android {
    namespace 'br.com.azmina.penhas'

    compileSdkVersion 35

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }


    defaultConfig {
        applicationId "br.com.azmina.penhas"
        minSdkVersion 24
        targetSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders["GEO_API_KEY"] = System.getenv('GEO_API_KEY') ?: secretsProperties.getProperty('GEO_API_KEY')
    }
    
    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17  // Set Java source compatibility to 17
        targetCompatibility = JavaVersion.VERSION_17  // Set Java target compatibility to 17
    }

    kotlinOptions {
        jvmTarget = "17"  // Set Kotlin to use Java 17
    }

    signingConfigs {
        release {
            keyAlias secretsProperties['keyAlias']
            keyPassword secretsProperties['keyPassword']
            storeFile secretsProperties['storeFile'] ? file(secretsProperties['storeFile']) : null
            storePassword secretsProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    lint {
        disable 'InvalidPackage'
    }

    applicationVariants.all { variant ->
        if (variant.name.contains("debug") || isFirebaseDistribution) {
            variant.mergedFlavor.applicationId = "dev.penhas.com.br"
        }
    }

    sourceSets {
        debug {
            res.srcDirs += ['src/dev/res']
        }
        release {
            res.srcDirs += isFirebaseDistribution ? ['src/dev/res'] : []
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}